if(isBuildAll.toBoolean()||isLibraryProject.toBoolean()){
    apply plugin: 'com.android.library'
}else {
    apply plugin: 'com.android.application'
}

apply plugin:'com.jakewharton.butterknife'

android {

    compileSdkVersion var.compileSdkVersion
    buildToolsVersion var.buildToolsVersion

    defaultConfig {
        minSdkVersion var.minSdkVersion
        targetSdkVersion var.targetSdkVersion
        versionCode var.versionCode
        versionName var.versionName
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }
    }



    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (isBuildAll.toBoolean() || isLibraryProject.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //We'll not compile the debug folder when building all.
                java {
                    exclude 'module/**'
                }
            } else {
                manifest.srcFile 'src/main/java/module/AndroidManifest.xml'
            }
        }
    }

    resourcePrefix "project_"

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    annotationProcessor "com.alibaba:arouter-compiler:1.1.4"
    implementation project(':library-common')

}
