if(isBuildAll.toBoolean()||isLibraryMain.toBoolean()){
    apply plugin: 'com.android.library'
}else {
    apply plugin: 'com.android.application'
}

apply plugin:'com.jakewharton.butterknife'

android {

    compileSdkVersion var.compileSdkVersion
    buildToolsVersion var.buildToolsVersion

    defaultConfig {
        minSdkVersion var.minSdkVersion
        targetSdkVersion var.targetSdkVersion
        versionCode var.versionCode
        versionName var.versionName
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [KEY_MODULE_NAME:"Main"]
                includeCompileClasspath true
            }
        }
    }

    sourceSets {
        main {
            if (isBuildAll.toBoolean() || isLibraryMain.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'

                //We'll not compile the debug folder when building all.
                java {
                    exclude 'module/**'
                }
            } else {
                manifest.srcFile 'src/main/java/module/AndroidManifest.xml'
            }
        }
    }

    resourcePrefix "main_"

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    annotationProcessor "com.jakewharton:butterknife-compiler:${versions.butterknife}"
    annotationProcessor "com.woaigmz.scm:scm-compiler:${versions.scm_compiler}"
    implementation project(':library-common')

    if(!isLibraryMain.toBoolean()){
        implementation project(':module-home')
        implementation project(':module-project')
        implementation project(':module-mine')
    }


}
